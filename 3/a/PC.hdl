// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    // get the incremented value
    Inc16(in=currentValue, out=incValue);

    // inc first, cuz its the last else if
    Mux16(a=currentValue, b=incValue, sel=inc, out=temp1);

    // load next
    Mux16(a=temp1, b=in, sel=load, out=temp2);

    // reset last
    Mux16(a=temp2, b=false, sel=reset, out=result);

    Register(in=result, load=true, out=out, out=currentValue);
}

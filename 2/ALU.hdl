// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    // if (zx) x = 0;
    Mux16(a=x, b=false, sel=zx, out=zxOut);

    // if (nx) x = !x;
    Not16(in=zxOut, out=notX);
    Mux16(a=zxOut, b=notX, sel=nx, out=xOut);

    // if (zy) y = 0;
    Mux16(a=y, b=false, sel=zy, out=zyOut);

    // if (ny) y = !y;
    Not16(in=zyOut, out=notY);
    Mux16(a=zyOut, b=notY, sel=ny, out=yOut);

    // if (f) out=x+y; else out=x&y;
    Add16(a=xOut, b=yOut, out=sumXY);
    And16(a=xOut, b=yOut, out=andXY);
    Mux16(a=andXY, b=sumXY, sel=f, out=tempOut);

    // if (no) out = !out;
    Not16(in=tempOut, out=notOut);
    Mux16(a=tempOut, b=notOut, sel=no, out=out,out[15]=outNg,out[0..7]=finalOutLeft,out[8..15]=finalOutRight);

    // if (out == 0) zr = 1; else zr = 0;
	Or8Way(in=finalOutLeft, out=zrOr1);
	Or8Way(in=finalOutRight, out=zrOr2);
    Or(a=zrOr1, b=zrOr2, out=outNotZero);
    Mux(a=true, b=false, sel=outNotZero, out=zr);

    // if (out < 0) ng = 1; else ng = 0;
    And(a=true, b=outNg, out=ng);
}